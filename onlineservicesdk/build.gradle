plugins {
    id 'com.android.library'
    id 'maven-publish'
}
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdkVersion 34
    buildToolsVersion "34.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 11

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            android.libraryVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "online_${variant.productFlavors[0].versionName}_${variant.productFlavors[0].name}_${variant.buildType.name}.aar"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "app"

    productFlavors {

        lib {
            dimension = "app"
            versionName = libVersionName
        }

        kit {
            dimension = "app"
            versionName = kitVersionName
        }
    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.2.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //lib版本依赖库
    implementation "org.jsoup:jsoup:1.12.1"
    //lib版本依赖模块
    embed project(path: ':mqtt', configuration: 'default')
    embed project(path: ':oslib', configuration: 'default')

    //kit版本依赖库
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation "com.github.smile525:WXCameraLibrary:1.0.8"  //拍照、摄像
    implementation 'com.github.smile525:AndroidVideoCache:1.0.1' //视频缓存框架
    compileOnly 'com.github.bumptech.glide:glide:4.11.0'//gif图片加载所用

    //kit版本依赖模块
    embed project(configuration: 'default', path: ':threepart')
    embed project(path: ':imagepicker', configuration: 'default')
    embed project(path: ':oswidget', configuration: 'default')
    embed project(path: ':oskit', configuration: 'default')
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            kitRelease(MavenPublication) {
                print("发布：kitRelease")


                from components.kitRelease
                //建议和github地址对应，否则对jitpack无效
                groupId = "${groupId}"
                //建议和github仓库名称对应，否则对jitpack无效
                artifactId = "${artifactId}"
                //建议和release的tag相同，否则对jitpack无效
                version = "${kitVersionName}"
            }

            libRelease(MavenPublication) {
                print("发布：libRelease")

                from components.libRelease
                //建议和github地址对应，否则对jitpack无效
                groupId = "${groupId}"
                //建议和github仓库名称对应，否则对jitpack无效
                artifactId = "${artifactId}"
                //建议和release的tag相同，否则对jitpack无效
                version = "${libVersionName}"
            }


//            release(MavenPublication) {
//                from components.release
//                //建议和github地址对应，否则对jitpack无效
//                groupId = "${outKitAar?groupId:libGroupId}"
//                //建议和github仓库名称对应，否则对jitpack无效
//                artifactId = "${outKitAar?artifactId:libArtifactId}"
//                //建议和release的tag相同，否则对jitpack无效
//                version = "${outKitAar?versionName:libVersionName}"
//            }
        }
    }
}